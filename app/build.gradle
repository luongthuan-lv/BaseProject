plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    namespace 'com.bigsoft.clap.myphone'
    compileSdk 33

    defaultConfig {
        applicationId "com.bigsoft.clap.myphone"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def BOOLEAN = "boolean"
        def STRING = "String"
        def TRUE = "true"
        def FALSE = "false"
        def DEV_MODE = "DEV_MODE"

        def URL_ENDPOINT = "URL_ENDPOINT"
        def ENDPOINT = "\"https://bzkconnect.com/\""
        def SERVER_API_CODE = "SERVER_API_CODE"
        def SERVER_API_CODE_RELEASE = "\"JMy6GAeGYgRmfCzG\""
        def SERVER_API_ACTION = "SERVER_API_ACTION"
        def SERVER_API_ACTION_RELEASE = "\"file-recovery-check-update\""

        debug {
            manifestPlaceholders = [admob_app_id: "ca-app-pub-3940256099942544~3347511713"]
            buildConfigField(BOOLEAN, DEV_MODE, TRUE)
            buildConfigField(STRING, URL_ENDPOINT, ENDPOINT)
            buildConfigField(STRING, SERVER_API_CODE, SERVER_API_CODE_RELEASE)
            buildConfigField(STRING, SERVER_API_ACTION, SERVER_API_ACTION_RELEASE)
            versionNameSuffix '-dev'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            manifestPlaceholders = [admob_app_id: "ca-app-pub-8285969735576565~3281667948"]
            buildConfigField(BOOLEAN, DEV_MODE, FALSE)
            buildConfigField(STRING, URL_ENDPOINT, ENDPOINT)
            buildConfigField(STRING, SERVER_API_CODE, SERVER_API_CODE_RELEASE)
            buildConfigField(STRING, SERVER_API_ACTION, SERVER_API_ACTION_RELEASE)
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    bundle {
        // split language = false
        language {
            enableSplit = false
        }

        // split multi screen = false
        density {
            enableSplit = false
        }

        abi {
            enableSplit = true
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Lifecycle
    def lifecycleVersion = '2.5.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.6.0-alpha01"

    // coroutines
    def coroutineVersion = '1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.2'

    // navigation component
    def nav_version = "2.5.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"


    // dots indicator
    implementation 'com.tbuonomo:dotsindicator:4.2'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'

    // In-app
    implementation 'com.android.billingclient:billing:5.1.0'
    implementation 'com.android.billingclient:billing-ktx:5.1.0'

    // Firebase performance
    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    implementation 'com.google.firebase:firebase-perf-ktx'

    // LIB AD - Mediation & Adapter
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation group: 'com.facebook.android', name: 'audience-network-sdk', version: '6.13.7'
    api 'com.google.android.gms:play-services-ads:22.0.0'

    implementation 'com.google.android.ads:mediation-test-suite:3.0.0'
    implementation 'com.google.ads.mediation:facebook:6.13.7.0'

    // Fb SDK - Analytics: https://github.com/facebook/facebook-android-sdk
    implementation 'com.facebook.android:facebook-core:[8,9)'

    //Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0@aar'

    // Rating bar
    implementation 'com.github.ome450901:SimpleRatingBar:1.5.1'

    // Circular Progress
    implementation 'com.github.antonKozyriatskyi:CircularProgressIndicator:1.3.0'

    implementation project(':libFirebaseTracker')
    implementation project(':libNetwork')
    implementation(project(':libAdManager')) {
        exclude group: 'com.facebook.android', module: 'audience-network-sdk'
        exclude group: 'com.google.android.gms', module: 'play-services-ads'
    }
    implementation(project(':libAdmob')) {
        exclude group: 'com.google.android.gms', module: 'play-services-ads'
        exclude group: 'com.google.ads.mediation', module: 'facebook'
    }
    implementation(project(':libFacebookAd')) {
        exclude group: 'com.facebook.android', module: 'audience-network-sdk'
    }
    implementation(project(':libIab')) {
        exclude group: 'androidx.appcompat', module: 'appcompat'
        exclude group: 'com.android.billingclient', module: 'billing'
        exclude group: 'com.android.billingclient', module: 'billing-ktx'
    }

    implementation project(':libUtils')
    implementation 'com.adjust.sdk:adjust-android:4.32.0'
    implementation(project(':libAdjustSdk')) {
        exclude group: 'com.adjust.sdk', module: 'adjust-android'
    }
    implementation project(':libCheckUpdateApp')
}